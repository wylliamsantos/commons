plugins {
	id 'java'
	id 'maven-publish'
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.1.2'
}

group = 'br.com.taskmanager'
version = getVersion()

java {
	sourceCompatibility = '19'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/wylliamsantos/commons")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME_GITHUB")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN_GITHUB")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			from(components.java)

			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
	}

}

def getVersion() {
	def gitDescribe = 'git describe --tags --abbrev=0'.execute().text.trim()
	def tag = gitDescribe.replaceAll('^v', '') // Remove o prefixo 'v' da tag (se existir)
	return tag
}